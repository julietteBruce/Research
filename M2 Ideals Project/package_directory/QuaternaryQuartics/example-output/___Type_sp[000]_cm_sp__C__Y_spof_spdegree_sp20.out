-- -*- M2-comint -*- hash: -332402360

i1 : kk = ZZ/31

o1 = kk

o1 : QuotientRing

i2 : U = kk[y0,y1,y2,y3,y4,y5,y6,y7];

i3 : setRandomSeed 8327432

o3 = 8327432

i4 : U1 = vars U

o4 = | y0 y1 y2 y3 y4 y5 y6 y7 |

             1       8
o4 : Matrix U  <--- U

i5 : P6 = matrix{{y1,y2,y3,y4,y5,y6,y7}} -- The hyperplane y0 == 0

o5 = | y1 y2 y3 y4 y5 y6 y7 |

             1       7
o5 : Matrix U  <--- U

i6 : F1 = P6 * random(kk^7,kk^5);

             1       5
o6 : Matrix U  <--- U

i7 : F2 = P6 * random(kk^7,kk^5);

             1       5
o7 : Matrix U  <--- U

i8 : F3 = P6 * random(kk^7,kk^5);

             1       5
o8 : Matrix U  <--- U

i9 : fg = transpose F1 | transpose F2 | transpose F3 ;

             5       3
o9 : Matrix U  <--- U

i10 : ff = matrix{{y0,0,0,0,0},{0,0,0,0,0},{0,0,0,0,0}};

              3       5
o10 : Matrix U  <--- U

i11 : fg = transpose fg + ff;    --3x5 matrix of rank 1 at (1:0:..:0), of rank 2 on fourfold Y

              3       5
o11 : Matrix U  <--- U

i12 : Y = fg3 = minors(3,fg);

o12 : Ideal of U

i13 : fg2 = minors(2,fg);

o13 : Ideal of U

i14 : codim fg3

o14 = 3

i15 : codim fg2, degree fg2

o15 = (7, 1)

o15 : Sequence

i16 : saturate fg2

o16 = ideal (y7, y6, y5, y4, y3, y2, y1)

o16 : Ideal of U

i17 : sf1 = fg^{0,1}

o17 = | y0+12y1+3y2+7y3+4y4+13y5+5y6-y7 -13y1+3y2-9y3-3y4-y5+2y6-2y7  
      | 9y1-7y2-13y3-15y4-2y5+8y6+15y7  -9y1-8y2+14y3-3y4-5y5+4y6+15y7
      -----------------------------------------------------------------------
      7y1+5y3-y4-6y5+5y6-6y7       -7y1-3y2+3y3-11y4-3y5-y6-5y7   
      7y1+9y2-5y3-2y4+6y5+15y6+6y7 14y1+12y2+5y3-6y4+15y5+13y6+4y7
      -----------------------------------------------------------------------
      9y1+4y2-5y3+3y4+4y5-14y6-13y7    |
      -4y1+8y2+12y3+14y4-13y5+3y6-10y7 |

              2       5
o17 : Matrix U  <--- U

i18 : sf2 = fg^{0,2}

o18 = | y0+12y1+3y2+7y3+4y4+13y5+5y6-y7 -13y1+3y2-9y3-3y4-y5+2y6-2y7
      | -6y1-7y2+14y3-7y4-9y5+2y6+9y7   9y1+9y2-2y3+12y4+7y5-2y6    
      -----------------------------------------------------------------------
      7y1+5y3-y4-6y5+5y6-6y7        -7y1-3y2+3y3-11y4-3y5-y6-5y7  
      -7y1+8y2+12y3+6y4-15y5+5y6-y7 8y1-10y2+5y3-11y4+9y5-11y6+3y7
      -----------------------------------------------------------------------
      9y1+4y2-5y3+3y4+4y5-14y6-13y7  |
      -5y1-15y2-8y3-4y4+5y5-12y6+3y7 |

              2       5
o18 : Matrix U  <--- U

i19 : Z1 = sf12 = minors(2,sf1);--threefold rational scroll of degree 5 in Y

o19 : Ideal of U

i20 : Z2 = sf22 = minors(2,sf2);--threefold rational scroll of degree 5 in Y

o20 : Ideal of U

i21 : sf = trim intersect(Z1, Z2);-- threefold rational scroll over reducible conic of degree 10 in Y

o21 : Ideal of U

i22 : (dim Z1, degree Z1)

o22 = (4, 5)

o22 : Sequence

i23 : betti sf  --in 11 cubics, so sf is in a cubic hypersurface section of Y

             0  1
o23 = total: 1 54
          0: 1  .
          1: .  .
          2: . 11
          3: . 43

o23 : BettiTally

i24 : k3 = ideal select(sf_*, f -> degree f == {3}); -- the cubics in the ideal

o24 : Ideal of U

i25 : k3=submatrix(gens sf,{0,1,2,3,4,5,6,7,8,9,10});  --the cubics in the ideal of sf

              1       11
o25 : Matrix U  <--- U

i26 : k3=ideal flatten k3;

o26 : Ideal of U

i27 : X = cy = (k3:Z1):Z2; --the CY threefold, residual to sf in a cubic hypersurface section

o27 : Ideal of U

i28 : betti X

             0  1
o28 = total: 1 16
          0: 1  .
          1: .  .
          2: . 16

o28 : BettiTally

i29 : betti res X

             0  1  2  3 4
o29 = total: 1 16 30 16 1
          0: 1  .  .  . .
          1: .  .  .  . .
          2: . 16 30 16 .
          3: .  .  .  . .
          4: .  .  .  . 1

o29 : BettiTally

i30 : (dim X, degree X)

o30 = (4, 20)

o30 : Sequence

i31 : J = jacobian X;

              8       16
o31 : Matrix U  <--- U

i32 : minors(4, sub(J, matrix{{1_kk,0,0,0,0,0,0,0}})) == 0

o32 = true

i33 : sub(X, matrix{{1_kk,0,0,0,0,0,0,0}})

o33 = ideal (0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0)

o33 : Ideal of kk

i34 : 
