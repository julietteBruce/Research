-- -*- M2-comint -*- hash: -769509267

i1 : kk = QQ;

i2 : U = kk[y0,y1,y2,y3,y4,y5,y6,y7];

i3 : P4a = ideal(y0,y1,y2); --a P4

o3 : Ideal of U

i4 : P4b = ideal(y4,y5,y6); --another P4

o4 : Ideal of U

i5 : P5 = ideal(y4,y5);-- a P5 containing P4b

o5 : Ideal of U

i6 : P6 = ideal(y6);--a P6 containing P4b

o6 : Ideal of U

i7 : CI222 = ideal(random(2,P4a),random(2,P4a),random(2,P4a));--a complete intersection (2,2,2) containing P4a

o7 : Ideal of U

i8 : Y7 = CI222:P4a; -- a 4-fold of degree 7 linked (2,2,2) to a P4a

o8 : Ideal of U

i9 : CI23 = ideal(random(2,Y7),random(3,Y7));--a complete intersection (2,3) that contains Y7

o9 : Ideal of U

i10 : Z6 = P4b + CI23; --a complete intersection (2,3) in P4b that contain the intersection of P4b and Y7

o10 : Ideal of U

i11 : X6 = P5 + ideal(random(2,Z6),random(3,Z6));-- a complete intersection (2,3) in P5 with hyperplane section Z6

o11 : Ideal of U

i12 : Y67 = intersect(Y7,Z6);--the union of the 4-fold Y7 and the Z6

o12 : Ideal of U

i13 : Y18 = CI23 + random(3,Y67);--a complete intersection (2,3,3) that contains Y7 and Z6

o13 : Ideal of U

i14 : Y11 = Y18:Y7;-- a 4-fold of degree 11 that contains Z6

o14 : Ideal of U

i15 : X11 = P6 + Y11; --a 3-fold of degree 11 that contains Z6

o15 : Ideal of U

i16 : X17 = intersect(X11,X6); -- a AG 3-fold in P7 of type (310)

o16 : Ideal of U

i17 : (dim X17, degree X17)

o17 = (4, 17)

o17 : Sequence

i18 : betti res X17

             0 1  2 3 4
o18 = total: 1 8 14 8 1
          0: 1 .  . . .
          1: . 3  1 . .
          2: . 5 12 5 .
          3: . .  1 3 .
          4: . .  . . 1

o18 : BettiTally

i19 : 
