needsPackage "Book3264Examples"

F1E0 =  P3 = flagBundle({1,3})

F1E1 =  O1 = dual(P3.Bundles#0)

F1E2 =  T = tangentBundle(P3)

F1E3 =  NX = O1^**4 -- the fourth tensor power of O(1), i.e. O(4)

F1E4 =  X = chern(1,NX) -- the fundamental class [X] of X

F1E5 =  TX = chern(T - NX) * X

F1E6 =  integral TX -- The Euler characteristic of a quartic surface

F1E7 =  eulerChar = n -> (

          S := base d;

          Pn := flagBundle({1,n},S);

          TPn := tangentBundle(Pn);

          O1 := dual(Pn.Bundles#0);

          NX := O1^**d;

          TX := chern(TPn - NX)*chern(1,NX);

          integral TX)

F1E8 =  eulerChar(4) -- The Euler characteristic of a degree-d hypersurface in P4

F1E9 =  sub(eulerChar(4),{d=>4/1}) -- The Euler characteristic of quartic threefold

F1E10 =  middleBetti = n -> (

           euC := eulerChar(n);

           ((-1)^(n-1)) * (euC - 2*ceiling((n-1)/2)))

F1E11 =  middleBetti(4) -- The middle Betti number of a degree-d hypersurface in P4

F1E12 =  sub(middleBetti(4), {d => 5/1}) -- The middle Betti number of a quintic threefold

F1E13 =  for n from 3 to 5 do (

           for e from 2 to 5 do (

                euC := sub(eulerChar(n),{d=>e/1});

                midB := sub(middleBetti(n),{d=>e/1});

                << "n: " << n << " d: " << e << " chi: " << euC << " middle Betti: " << midB << endl))

F1E14 =  eulerChar = (n,k) -> (

           S := base(e_1 .. e_k);

           Pn := flagBundle({1,n},S);

           TPn := tangentBundle(Pn);

           O1 := dual(Pn.Bundles#0);

           N := sum(1..k, i-> O1^**(e_i)); --the denominator in the above formula

           X := product(1..k, i->chern(1,O1^**(e_i))); --fundamental class of X

           TX := chern(TPn - N) * X;

           integral TX)

F1E15 =  eulerChar(4,2) -- Euler char of a complete intersection surface in P4

F1E16 =  middleBetti = (n,k) -> (

           euC := eulerChar(n,k);

           ((-1)^(n-k)) * (euC - 2*ceiling((n-k)/2)))

F1E17 =  sub(middleBetti(4,2),{e_1=>2,e_2=>3/1}) -- complete intersection of a quadric and cubic in P4

F1E18 =  sub(middleBetti(5,3),{e_1=>2,e_2=>2,e_3=>2/1}) -- three quadrics in P5

F1E19 =  sub(eulerChar(4,2),{e_1=>2,e_2=>3/1}) -- complete intersection of a quadric and cubic in P4

F1E20 =  sub(eulerChar(5,3),{e_1=>2,e_2=>2,e_3=>2/1}) -- three quadrics in P5

F1E21 =  sub(middleBetti(5,2),{e_1=>2,e_2=>2/1})

F1E22 =  P2 = flagBundle({1,2})

F1E23 =  P2xP3 = flagBundle({1,3},P2,VariableNames => K)

F1E24 =  intersectionRing(P2xP3)

F1E25 =  eulerChar = (n,m) -> (

           S := base(a,b);

           Pn := flagBundle({1,n},S);

           PnxPm := flagBundle({1,m},Pn);

           T := tangentBundle(PnxPm);

           O1Pn := dual(Pn.Bundles#0);

           f := PnxPm / Pn; -- the first projection map from P2xP3 to P2

           O10 := f^* O1Pn; -- we pull back O_P2(1) to get O(1,0)

           O01 := dual(PnxPm.Bundles#0); -- O(0,1)

           NX := (O10^**a)**(O01^**b); -- O(a,b)

           X := chern(1,NX); -- Chow class of divisor of type (a,b)

           TX := chern(T - NX) * X; -- pushed-forward total chern class of tangent bundle to X

           integral TX) -- chi of a smooth hypersurface of bidegree (a,b) in PnxPm

F1E26 =  eulerChar(4,4) -- chi of a smooth hypersurface of bidegree (a,b) in P4xP4

F1E27 =  sub(eulerChar(2,3),{a=>1,b=>0/1}) -- is P1xP3, should be 8 by Kunneth

F1E28 =  sub(eulerChar(1,1),{a=>1,b=>1/1}) -- a conic in P2, should be 2

F1E29 =  sub(eulerChar(1,1),{a=>2,b=>1/1}) -- a twisted cubic, should be 2

exampleIDS = {"F1E0", "F1E1", "F1E2", "F1E3", "F1E4", "F1E5", "F1E6", "F1E7", "F1E8", "F1E9", "F1E10", "F1E11", "F1E12", "F1E13", "F1E14", "F1E15", "F1E16", "F1E17", "F1E18", "F1E19", "F1E20", "F1E21", "F1E22", "F1E23", "F1E24", "F1E25", "F1E26", "F1E27", "F1E28", "F1E29"}
